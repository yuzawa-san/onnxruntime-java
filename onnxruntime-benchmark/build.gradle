plugins {
	id "me.champeau.jmh" version "0.6.8"
	id "io.morethan.jmhreport" version "0.9.0"
}
apply plugin: "application"


dependencies {
	jmh project(":")
	jmh project(path: ":", configuration: 'cpu')
	jmh project(':onnx')
	jmh "com.microsoft.onnxruntime:onnxruntime:1.13.1"
	// jmh "org.bytedeco:onnxruntime-platform:1.13.1-1.5.8"
	jmh 'org.openjdk.jmh:jmh-core:1.36'
	jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

application {
	mainClass = 'com.jyuzawa.onnxruntime_benchmark.Benchmark'
	applicationDefaultJvmArgs = ['--enable-preview', '--enable-native-access=ALL-UNNAMED']
}

task benchmark(type: Exec) {
	commandLine "${projectDir}/benchmark.sh"
	dependsOn installDist
}

jmh {
	resultFormat = "JSON"
	resultsFile = project.file("${project.buildDir}/reports/jmh/result.json")
	profilers = ["gc"] //, "async:libPath=/Users/jtyuzawa/Downloads/async-profiler-2.9-macos/build/libasyncProfiler.dylib"]
}

jmhReport {
	jmhResultPath = project.file("${project.buildDir}/reports/jmh/result.json")
	jmhReportOutput = project.file("${project.buildDir}/reports/jmh")
}
tasks.jmh.finalizedBy tasks.jmhReport