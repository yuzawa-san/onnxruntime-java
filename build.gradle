plugins {
	id "com.diffplug.spotless" version "6.12.0"
	id 'java'
	id 'maven-publish'
	id 'signing'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
	id "com.google.protobuf" version "0.9.1"
	id "com.github.jakemarsden.git-hooks" version "0.0.2"
	id 'net.researchgate.release' version '3.0.2'
}

apply plugin: "java-library"

release {
	tagTemplate = 'v${version}'
	git {
		requireBranch.set('master')
		signTag.set(true)
	}
}
afterReleaseBuild.dependsOn publish

allprojects {
	apply plugin: "java"
	apply plugin: "com.diffplug.spotless"
	sourceCompatibility = targetCompatibility = 19
	tasks.withType(JavaCompile) {
		options.compilerArgs += "--enable-preview"
	}
	repositories {
		mavenCentral()
	}
	spotless {
		java {
			target '**/*.java'
			targetExclude "${project.buildDir}/**/*.java"
			licenseHeader '/*\n * Copyright (c) $YEAR James Yuzawa (https://www.jyuzawa.com/)\n * SPDX-License-Identifier: MIT\n */'
			removeUnusedImports()
			importOrder()
			palantirJavaFormat()
			endWithNewline()
		}
		format 'misc', {
			target '**/*.gradle', '**/*.proto', '**/*.feature'
			trimTrailingWhitespace()
			indentWithTabs()
			endWithNewline()
		}
		format 'md', {
			target '**/*.md'
			trimTrailingWhitespace()
			endWithNewline()
		}
	}
}

gitHooks {
	hooks = ['pre-push': 'spotlessCheck']
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: "com/jyuzawa/onnxruntime_extern/**")
		}))
	}
}

description = "A Java binding of Microsoft's ONNX Runtime project."

// NOTE: to bump ORT version, update gradle.properties then run `./gradlew clean jextractClean jextract`
def ORT_VERSION = project.findProperty('com.jyuzawa.onnxruntime.library_version')

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def jarAttributes = [
	'Built-By'               : System.properties['user.name'],
	'Build-Timestamp'        : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
	'Specification-Vendor'   : 'yuzawa-san',
	'Specification-Title'    : project.name,
	'Specification-Version'  : project.version,
	'Implementation-Vendor'  : 'yuzawa-san',
	'Implementation-Title'   : project.name,
	'Implementation-Version' : "${-> getGitHash()}",
	'Onnxruntime-Version'    : ORT_VERSION,
	'Created-By'             : "Gradle ${gradle.gradleVersion}",
	'Build-Jdk'              : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
	'Build-OS'               : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
]

java {
	withJavadocJar()
	withSourcesJar()
}

def cpuOsArches = ["linux-x86_64", "linux-aarch_64", "windows-x86_64", "windows-aarch_64", "osx-x86_64", "osx-aarch_64"]
def gpuOsArches = ["linux-x86_64-gpu", "windows-x86_64-gpu"]
def allArches = cpuOsArches + gpuOsArches

allArches.each { osArch ->
	def osArchRoot = "${project.buildDir}/onnxruntime-${ORT_VERSION}/${osArch}"
	task "download${osArch}" (type: Exec) {
		outputs.cacheIf { true }
		inputs.property('ortVersion', ORT_VERSION)
		inputs.property('osArch', osArch)
		inputs.file('./download.sh')
		environment('ORT_VERSION', ORT_VERSION)
		environment('VARIANT', osArch)
		commandLine 'sh','./download.sh'
		outputs.dir("${osArchRoot}/include")
		outputs.dir("${osArchRoot}/assembly")
	}
	task "copy${osArch}" (type: Copy) {
		dependsOn tasks.named("download${osArch}")
		from "${osArchRoot}/assembly"
		into "${project.buildDir}/dst/${osArch}"
	}
	task "osArchJar${osArch}" (type: Jar) {
		from tasks.named("copy${osArch}")
		classifier osArch
		doFirst {
			manifest {
				attributes(jarAttributes)
			}
		}
		from ("${project.projectDir}/src/dist")
		from ("${osArchRoot}/LICENSE") {
			rename "LICENSE", "LICENSE.onnxruntime.txt"
			into "META-INF/licenses/"
		}
	}
}

// TODO: enable jextract

task jextract(type: Exec) {
	environment('ORT_VERSION', ORT_VERSION)
	commandLine 'sh','./jextract.sh'
	dependsOn tasks.named("downloadlinux-x86_64-gpu")
	finalizedBy tasks.named('spotlessJavaApply')
	// TODO: enable jextract
	// outputs.dir("${project.buildDir}/generated/source/jextract")
}
// TODO: enable jextract
//compileJava.dependsOn(jextract)

configurations {
	cpu {
		cpuOsArches.each {
			outgoing.artifact(tasks.named("osArchJar${it}"))
		}
	}
	nativeElements {
		canBeResolved = false
		canBeConsumed = true
		attributes {
			attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
			attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
			attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.DYNAMIC_LIB))

		}
		allArches.each {
			outgoing.artifact(tasks.named("osArchJar${it}"))
		}
	}
}
components.java {
	addVariantsFromConfiguration(configurations.nativeElements) {
		mapToOptional()
	}
}

def makePom(node, osArches) {
	def parentNode = node.appendNode('parent')
	parentNode.appendNode('groupId', project.group)
	parentNode.appendNode('artifactId', project.name)
	parentNode.appendNode('version', version)
	def dependenciesNode = node.appendNode('dependencies')
	def mainDependency = dependenciesNode.appendNode('dependency')
	mainDependency.appendNode('groupId', project.group)
	mainDependency.appendNode('artifactId', project.name)
	mainDependency.appendNode('version', version)
	osArches.each {
		def dependency = dependenciesNode.appendNode('dependency')
		dependency.appendNode('groupId', project.group)
		dependency.appendNode('artifactId', project.name)
		dependency.appendNode('version', version)
		dependency.appendNode('classifier', it)
	}
}

project.group = "com.jyuzawa"
def REPO = "yuzawa-san/onnxruntime-java"
publishing {
	repositories {
		maven {
			name = 'Maven'
			url = uri(version.endsWith('-SNAPSHOT') ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/' : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
			credentials {
				username = project.findProperty("ossrhUsername")
				password = project.findProperty("ossrhPassword")
			}
		}
	}
	publications {
		onnxruntimeCpu(MavenPublication) {
			artifactId = "${rootProject.name}-cpu"
			pom.withXml {
				makePom(asNode(), cpuOsArches)
			}
		}
		onnxruntimeGpu(MavenPublication) {
			artifactId = "${rootProject.name}-gpu"
			pom.withXml {
				makePom(asNode(), gpuOsArches)
			}
		}
		onnxruntime(MavenPublication) {
			from components.java
			pom {
				name = 'ONNX Runtime for Java'
				inceptionYear = '2022'
				description = project.description
				url = "https://github.com/${REPO}"
				issueManagement {
					url = "https://github.com/${REPO}/issues"
					system = "GitHub Issues"
				}
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
						distribution = 'repo'
					}
				}
				developers {
					developer {
						id = 'yuzawa-san'
						name = 'James Yuzawa'
						email = 'jtyuzawa@gmail.com'
					}
				}
				scm {
					connection = "scm:git:git://github.com/${REPO}.git"
					developerConnection = "scm:git:git@github.com:${REPO}.git"
					url = "https://github.com/${REPO}"
				}
			}
		}
	}
}

signing {
	sign publishing.publications
}

dependencies {
	testImplementation "junit:junit:4.13.2"
	testImplementation 'com.google.protobuf:protobuf-java:3.21.9'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.19.0'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-jpl:2.19.0'
}

sourceSets {
	main {
		java {
			// TODO: enable jextract
			// srcDirs jextract
		}
	}
	test {
		resources {
			cpuOsArches.each { osArch ->
				srcDir tasks.named("copy${osArch}")
			}
		}
	}
}

java {
	modularity.inferModulePath = true
}

javadoc {
}
javadoc.options {
	addBooleanOption('-enable-preview', true)
	addStringOption('-release', '19')
}

def moduleName = "com.jyuzawa.onnxruntime"
jar {
	doFirst {
		manifest {
			attributes(jarAttributes)
		}
	}
	from ("${project.projectDir}/LICENSE") {
		into "META-INF/"
	}
	into("META-INF/maven/$project.group/$project.name") {
		from { generatePomFileForOnnxruntimePublication }
		rename ".*", "pom.xml"
	}
}

tasks.named('compileJava') {
	// use the project's version or define one directly
	options.javaModuleVersion = provider { project.version }
}


test {
	jvmArgs '--enable-preview', '--enable-native-access=ALL-UNNAMED'
	finalizedBy jacocoTestReport
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.21.9"
	}
	generateProtoTasks {
		ofSourceSet('test').each { task -> sourceSets.test.java.srcDirs files("${protobuf.generatedFilesBaseDir}/test/java").builtBy(task) }
	}
}
