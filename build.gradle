plugins {
	id "com.diffplug.spotless" version "6.2.1"
	id 'java'
	id 'maven-publish'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
}

apply plugin: "java-library"

spotless {
	java {
		target '**/*.java'
		targetExclude "${project.buildDir}/**/*.java"
		licenseHeader '/*\n * Copyright (c) $YEAR James Yuzawa (https://www.jyuzawa.com/)\n * All rights reserved. Licensed under the MIT License.\n */'
		removeUnusedImports()
		importOrder()
		palantirJavaFormat()
		endWithNewline()
	}
	format 'misc', {
		target '**/*.gradle', '**/*.proto', '**/*.feature'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	format 'md', {
		target '**/*.md'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: "com/jyuzawa/onnxruntime/extern/**")
		}))
	}
}
check.dependsOn jacocoTestReport

sourceCompatibility = targetCompatibility = 17

def ORT_VERSION = project.findProperty('com.jyuzawa.onnxruntime.library_version')
def BUILD_VERSION = project.findProperty('com.jyuzawa.onnxruntime.build_version')
version = "${ORT_VERSION}-${BUILD_VERSION}"

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	repositories {
		maven {
			name = 'GitHub'
			url = uri("https://maven.pkg.github.com/yuzawa-san/onnxruntime-java")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = 'com.jyuzawa'
			artifactId = 'onnxruntime-java'
			from components.java
		}
	}
}

dependencies {
	testImplementation "junit:junit:4.13.2"
	api "io.netty:netty-common:4.1.73.Final"
	api 'org.slf4j:slf4j-api:1.7.35'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.1'
}

task make(type: Exec) {
	commandLine 'make', "ORT_VERSION=${ORT_VERSION}"
}

task jextract(type: Exec) {
	commandLine 'make', 'onnxruntime_all.h.conf', "ORT_VERSION=${ORT_VERSION}"
}

sourceSets {
	main {
		java {
			srcDirs += "${project.buildDir}/generated/jextract/"
		}
	}
}

compileJava {
	dependsOn make
}

tasks.withType(AbstractCompile) {
options.compilerArgs += ["--add-modules", "jdk.incubator.foreign"]
}

tasks.withType(Test) {
jvmArgs += ["--add-modules", "jdk.incubator.foreign"]
}

javadoc {
	exclude 'com/jyuzawa/onnxruntime/extern/**'
	options.addStringOption("-add-modules","jdk.incubator.foreign")
}

jar {
	from "${project.buildDir}/jnioutput"
}

test {
	jvmArgs '--enable-native-access=ALL-UNNAMED', '--add-modules', 'jdk.incubator.foreign'
}
