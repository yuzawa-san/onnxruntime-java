plugins {
	id "com.diffplug.spotless" version "6.2.1"
	id 'java'
	id 'maven-publish'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
	id "com.google.protobuf" version "0.8.18"
}

apply plugin: "java-library"

spotless {
	java {
		target '**/*.java'
		targetExclude "${project.buildDir}/**/*.java"
		licenseHeader '/*\n * Copyright (c) $YEAR James Yuzawa (https://www.jyuzawa.com/)\n * All rights reserved. Licensed under the MIT License.\n */'
		removeUnusedImports()
		importOrder()
		palantirJavaFormat()
		endWithNewline()
	}
	format 'misc', {
		target '**/*.gradle', '**/*.proto', '**/*.feature'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	format 'md', {
		target '**/*.md'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: "com/jyuzawa/onnxruntime_extern/**")
		}))
	}
}

sourceCompatibility = targetCompatibility = 17

def ORT_VERSION = project.findProperty('com.jyuzawa.onnxruntime.library_version')

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

def cpuOsArches = ["linux-x86_64", "linux-aarch_64", "windows-x86_64", "windows-aarch64", "osx-x86_64", "osx-aarch_64"]
def gpuOsArches = ["linux-x86_64-gpu", "windows-x86_64-gpu"]
def allArches = cpuOsArches + gpuOsArches

allArches.each { osArch ->
	task "osArchJar${osArch}" (type: Jar) {
		from "${project.buildDir}/jnioutput/${osArch}"
		classifier osArch
	}
}

def makePom(node, osArches) {
	def parentNode = node.appendNode('parent')
	parentNode.appendNode('groupId', project.group)
	parentNode.appendNode('artifactId', project.name)
	parentNode.appendNode('version', version)
	def dependenciesNode = node.appendNode('dependencies')
	def mainDependency = dependenciesNode.appendNode('dependency')
	mainDependency.appendNode('groupId', project.group)
	mainDependency.appendNode('artifactId', project.name)
	mainDependency.appendNode('version', version)
	osArches.each {
		def dependency = dependenciesNode.appendNode('dependency')
		dependency.appendNode('groupId', project.group)
		dependency.appendNode('artifactId', project.name)
		dependency.appendNode('version', version)
		dependency.appendNode('classifier', it)
	}
}

project.group = "com.jyuzawa"
def REPO = "github.com/yuzawa-san/onnxruntime-java"
publishing {
	repositories {
		maven {
			name = 'GitHub'
			url = uri("https://maven.pkg.${REPO}")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		onnxruntimeCpu(MavenPublication) {
			artifactId = "${rootProject.name}-cpu"
			pom.withXml {
				makePom(asNode(), cpuOsArches)
			}
		}
		onnxruntimeGpu(MavenPublication) {
			artifactId = "${rootProject.name}-gpu"
			pom.withXml {
				makePom(asNode(), gpuOsArches)
			}
		}
		onnxruntime(MavenPublication) {
			from components.java
			allArches.each {
				artifact tasks.named("osArchJar${it}")
			}
			pom {
				name = 'ONNX Runtime for Java'
				description = "A Java binding of Microsoft's onnxruntime project."
				url = "https://${REPO}"
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/mit-license.php'
						distribution = 'repo'
					}
				}
				developers {
					developer {
						id = 'yuzawa-san'
						name = 'James Yuzawa'
						email = 'jtyuzawa@gmail.com'
					}
				}
				scm {
					connection = "scm:git:git://${REPO}.git"
					developerConnection = "scm:git:ssh://${REPO}.git"
					url = REPO
				}
			}
		}


	}
}

dependencies {
	testImplementation "junit:junit:4.13.2"
	testImplementation 'com.google.protobuf:protobuf-java:3.19.4'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-jpl:2.17.1'
}

task make(type: Exec) {
	commandLine 'make', "ORT_VERSION=${ORT_VERSION}"
}

task jextract(type: Exec) {
	commandLine 'make', 'onnxruntime_all.h.conf', "ORT_VERSION=${ORT_VERSION}"
}

sourceSets {
	main {
		java {
			srcDirs += "${project.buildDir}/generated/jextract/"
		}
	}
	test {
		java {
			srcDirs "${protobuf.generatedFilesBaseDir}/test/java"
		}
		resources {
			cpuOsArches.each { osArch ->
				srcDir "${project.buildDir}/jnioutput/${osArch}"
			}
		}
	}
}

compileJava {
	dependsOn make
}

java {
	modularity.inferModulePath = true
}

javadoc {
}

def moduleName = "com.jyuzawa.onnxruntime"
jar {
	manifest {
		attributes('Automatic-Module-Name': moduleName)
	}
}

tasks.named('compileJava') {
	// use the project's version or define one directly
	options.javaModuleVersion = provider { project.version }
}


test {
	jvmArgs "--enable-native-access=ALL-UNNAMED", '--add-modules', 'jdk.incubator.foreign'
	finalizedBy jacocoTestReport
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.19.4"
	}
	generateProtoTasks {
		all().each { task -> }
		ofSourceSet('test')
	}
}
